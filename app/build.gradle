/*
 * Copyright (c) 2016-present. 贵州纳雍穿青人李裕江 and All Contributors.
 *
 * The software is licensed under the Mulan PSL v2.
 * You can use this software according to the terms and conditions of the Mulan PSL v2.
 * You may obtain a copy of Mulan PSL v2 at:
 *     http://license.coscl.org.cn/MulanPSL2
 * THIS SOFTWARE IS PROVIDED ON AN "AS IS" BASIS, WITHOUT WARRANTIES OF ANY KIND, EITHER EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO NON-INFRINGEMENT, MERCHANTABILITY OR FIT FOR A PARTICULAR
 * PURPOSE.
 * See the Mulan PSL v2 for more details.
 */

plugins {
    id 'com.android.application'
}

def getGitCommitCount() {
    def process = 'git rev-list --count HEAD'.execute()
    process.waitFor()
    return process.exitValue() == 0 ? process.text.trim().toInteger() : 0
}

def getGitDescribe() {
    def process = 'git describe --tags --always'.execute()
    process.waitFor()
    return process.exitValue() == 0 ? process.text.trim() : 'unknown'
}

dependencies {
    implementation project(':core')
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.7.1'
}

android {
    namespace 'com.android.msa'
    compileSdk rootProject.ext.compileSdk

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.targetSdk
        versionCode getGitCommitCount()
        versionName "${getGitCommitCount()}-${getGitDescribe()}"
        //应用的实际包名
        applicationId "com.android.msa"
        // 配合 productFlavors 节点按不同维度打包
        flavorDimensions = ["MODE"]
        // 修改 AndroidManifest.xml 里的变量，占位值无法覆盖的坑可参阅 https://www.jianshu.com/p/1d5271c2c366
        manifestPlaceholders = [
                MY_CHANNEL: "unknown",
        ]
    }

    signingConfigs {
        release {
            storeFile file("../android.keystore")
            storePassword '123456'
            keyAlias 'android'
            keyPassword '123456'
        }
        debug {
            storeFile file("../android.keystore")
            storePassword '123456'
            keyAlias 'android'
            keyPassword '123456'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            jniDebuggable = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all {
                variant ->
                    variant.outputs.all {
                        outputFileName = "${applicationId}_${defaultConfig.versionCode}.apk"
                    }
            }
        }
    }
   
}
